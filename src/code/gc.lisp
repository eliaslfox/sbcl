;;;; garbage collection and allocation-related code

;;;; This software is part of the SBCL system. See the README file for
;;;; more information.
;;;;
;;;; This software is derived from the CMU CL system, which was
;;;; written at Carnegie Mellon University and released into the
;;;; public domain. The software is in the public domain and is
;;;; provided with absolutely no warranty. See the COPYING and CREDITS
;;;; files for more information.

(in-package "SB-KERNEL")

;;;; DYNAMIC-USAGE and friends

(defun dynamic-space-obj-p (x) ; X must not be an immediate object
  (with-pinned-objects (x)
    (let ((addr (get-lisp-obj-address x)))
      (< sb-vm:dynamic-space-start addr (sap-int (dynamic-space-free-pointer))))))

(declaim (inline dynamic-usage))
(defun dynamic-usage ()
  #+gencgc
  (extern-alien "bytes_allocated" os-vm-size-t)
  #-gencgc
  (truly-the word
             (- (sap-int (sb-c::dynamic-space-free-pointer))
                sb-vm:dynamic-space-start)))

(defun static-space-usage ()
  (- (sap-int sb-vm:*static-space-free-pointer*) sb-vm:static-space-start))

(defun read-only-space-usage ()
  (- (sap-int sb-vm:*read-only-space-free-pointer*) sb-vm:read-only-space-start))

(defun control-stack-usage ()
  #-stack-grows-downward-not-upward
  (sap- (control-stack-pointer-sap) (descriptor-sap sb-vm:*control-stack-start*))
  #+stack-grows-downward-not-upward
  (sap- (descriptor-sap sb-vm:*control-stack-end*) (control-stack-pointer-sap)))

(defun binding-stack-usage ()
  (sap- (binding-stack-pointer-sap) (descriptor-sap sb-vm:*binding-stack-start*)))

;;;; GET-BYTES-CONSED

;;; the total number of bytes freed so far (including any freeing
;;; which goes on in PURIFY)
;;;
;;; (We save this so that we can calculate the total number of bytes
;;; ever allocated by adding this to the number of bytes currently
;;; allocated and never freed.)
(declaim (type unsigned-byte *n-bytes-freed-or-purified*))
(define-load-time-global *n-bytes-freed-or-purified* 0)
(defun gc-reinit ()
  (setq *gc-inhibit* nil)
  ;; This GC looks utterly unnecessary, but when I tried removing it,
  ;; I got heap exhaustions sooner than when I left it in.
  ;; So why is that? Well, it seems like there's some state that is getting
  ;; saved into the core which needs resetting, such as these SETQs,
  ;; but how is that affecting the C runtime's decision on when to
  ;; perform the first auto-triggered GC? How is not doing an explicit
  ;; GC delaying the decision? It seems wrong.
  (gc)
  (setf *n-bytes-freed-or-purified* 0
        *gc-run-time* 0))

(declaim (ftype (sfunction () unsigned-byte) get-bytes-consed))
(defun get-bytes-consed ()
  "Return the number of bytes consed since the program began. Typically
this result will be a consed bignum, so if you have an application (e.g.
profiling) which can't tolerate the overhead of consing bignums, you'll
probably want either to hack in at a lower level (as the code in the
SB-PROFILE package does), or to design a more microefficient interface
and submit it as a patch."
  (+ (dynamic-usage)
     *n-bytes-freed-or-purified*))

(defun primitive-object-size (object)
  "Return number of bytes of heap or stack directly consumed by OBJECT"
  (cond ((not (sb-vm:is-lisp-pointer (get-lisp-obj-address object))) 0)
        ((eq object nil) (ash sb-vm::sizeof-nil-in-words sb-vm:word-shift))
        ((simple-fun-p object) (code-object-size (fun-code-header object)))
        #-(or x86 x86-64 arm64 riscv) ((lra-p object) 1)
        (t
         (with-alien ((sizer (function unsigned unsigned) :extern "primitive_object_size"))
           (with-pinned-objects (object)
             (alien-funcall sizer (get-lisp-obj-address object)))))))

(defun sb-vm:hexdump (thing &optional (n-words nil wordsp)
                            ;; pass NIL explicitly if T crashes on you
                            (decode t))
  (multiple-value-bind (obj addr count)
      (if (typep thing 'word) ; ambiguous in the edge case, but assume it's
          ;; an address (though you might be trying to dump a bignum's data)
          (values nil thing (if wordsp n-words 1))
          (values
           thing
           (logandc2 (get-lisp-obj-address thing) sb-vm:lowtag-mask)
           (if wordsp
               n-words
               (if (and (typep thing 'code-component) (plusp (code-n-entries thing)))
                   ;; Display up through the first fun header
                   (+ (code-header-words thing)
                      (ash (%code-fun-offset thing 0) (- sb-vm:word-shift))
                      sb-vm:simple-fun-insts-offset)
                   ;; at most 16 words
                   (min 16 (ash (primitive-object-size thing) (- sb-vm:word-shift)))))))
    (with-pinned-objects (obj)
      (dotimes (i count)
        (let ((word (sap-ref-word (int-sap addr) (ash i sb-vm:word-shift))))
          (multiple-value-bind (lispobj ok fmt)
              (cond ((and (typep thing 'code-component)
                          (< 1 i (code-header-words thing)))
                     (values (code-header-ref thing i) t))
                    #+compact-symbol
                    ((and (typep thing '(and symbol (not null)))
                          (= i sb-vm:symbol-name-slot))
                     (values (list (sb-impl::symbol-package-id thing)
                                   (symbol-name thing))
                             t
                             "{连誉┅＋轫盹忾戾泔溴è犷豉疱翳轭Ж犷簌礅镬铒铛祆┅筲鲰后礅镬骢钽箪雉┅戾舄è轭溴筲愫烘钺礤扉铍弪轭溴翳轭绌ㄥ铘蝙痫轭筢瓠蝈姝黠蜾ㄥ翦蝾犰殄㈧轶疬扉铍徵暹翎忪澧簌篝屙狎遽痫轭翦颟ㄡ箬轭溴筲鲰瑚矧洵箬殒舂┅翎珑邃痿ㄡ扉孱骢钽犰ㄥ翦蝾犰殄㈠铘蝙痫轭暨翎珑邃痿颌ㄦ躅泗轱躅箝珙邃躅箝珙邃┅孱趄痫轭舂镡ē磲脲扉箴镡翎珑邃痿颟┅ㄩㄥ耢轭溴癌鲠祯弩ㄩㄦ怙躅漯翳轭绌簌礅镬骢钽糸镱翳轭绌立鲠祯弩扉篝轭溴孱趄痫轭镡ㄩㄦ怙躅漯翳轭绌簌礅镬骢钽糸镱翳轭绌┅Ⅺｘ睾ｘ亟连窿┅┅＋轫盹忾戾泔溴è犷豉疱翳轭筲脲蝾屐烘溴骖筲鲰烘溴骖骢瞽箪雉┅鲠祯弩扉篝筲愫烘钺礤扉铍弪轭溴翳轭绌ㄦ溴骖骢翳轭绌Ⅺｘ噩窿┅ㄤ邈镤ㄩ黠蜾筲鲰乎钼秕钿磲螂弪鏖溴翎绌鲠祯弩Ⅴ钼秕钿幄磲脲扉箴镡黠蜾铋飑┅戾è痱轭舡扉铄螵暴í痱轭舡痱弭豉舂ㄦ矧磲霈О痕揽蔺アǐ徜潋ㄡ箬筲鲰瑚矧洵箬殒舂í筲鲰侯黠蜾怡翦螬黠蜾镫矧骓英扉箴镡戛┅┅┅换换敲栾镫ㄤ彐轭瀛祜徜糸礤珈镡犰徭翦颦玢栾镫螵铋⒚犰戾徭翦遽汨玑蜮徵泔祆邈糸镱屮沐痿骘玑蜮徵泔祆邈糸镱趄殓珏蝈漉蜷铉翳蝈徜屮轸螽深眭祠轸栩遽溴孱鲩蝻铐孱翳弩栾镫磲蝓轭犷翳蝈徜换换轭翦蝾犰敲ㄤ彐轭瀛犰殄瞽蝻豸轭泔祆邈舡玑蜮徵轭灬篝珏轭舂＋矧筲翳蝈徜筲筢驽痫轭舂痱镧ㄤ彐轭瀛犰殄瞽蝻豸轭玢篝镳翳瀛黠蜢鲲殇ㄤ彐轭瀛犰殄瞽蝻豸轭玢篝狎舡翳瀛黠蜢鲲殇┅－矧筲翳蝈徜筲筢驽痫轭舂痱镧ㄤ彐躅玢篝镳翳瀛黠蜢īㄤ彐躅玢篝狎舡翳瀛黠蜢ī┅ㄤ邈灬轫ㄩ铎轭澌钺黹悱箴徙瀛箝濠ㄤ彐躅澌钺黹悱箴徙瀛箝ī⒂辁镦翳澌钺黹箴徙轭怡翦螽ㄥ翦蝾犰殄澌钺黹氵箴徙暹箝澧矬鲰箝瀛舂换换诱颅敲换诱颅敲滹弩玑蜮徵泔祆邈糸镱澡轶轶汜祆邃骝镯翳蝈痨徙弩换ū澡蝓铘轫鏖祆汜祆桢蝈麒孱轸溴翦泗翳狒麇鲥泔铙邃换孱秕玷麸屮沐邃翳玢趄殓珏翳蝈箬镬洚澡轶轶滹铄轭换犰祜悒骘珏钽玢矧轭翦蝌躔暨磲忮哏悒骘汨孱妁玢换ú澡躞弪磲蝈聃弩泔祆邈糸镱躞轭敲忮祜换ǔ留翳孱镦咨匀险原敲晌箦泗轱瞵麇狎汜祆邃殒换闻拍韵孟烫琶原橇衣燎弄轶趄蹂换换澡轶轶溟骀弪孱骝镯翳忮栳鲩秕轭爱犷遽蜢殄蚝轸铒换祜铉弪溴汩溴麒弭桢麸敲忉箦镱翳蝈箬镬潴涉秕汜祆换诱颅敲秕鏖祆溴骈铋翦禊珏敲彘翳弪铒矧麒孱翳换咨匀险原敲晌轶秭弪换骑桥蚊敲犰珏铄蜥糸镱桥鏖祆忮敲у洚ㄤ彐磲泸趄徙聃轵瀛玢祜汶é蝈篝骘蝽螬－筲翳蝈徜啜痱镧梨矧眢舂＋筲翳蝈徜啜麒孱ㄥ耢ㄡ扉孱骢钽犰ㄥ翦蝾犰殄Ⅳ蝙哚泷蹰蝈哏氵祜汶ㄦ躅泗轱轭舂┅暴梨矧眢ㄡ扉孱骢钽犰ㄥ翦蝾犰殄Ⅱ屐遽箦哏氵祜汶ㄦ躅泗轱鲲殇┅舂ㄤ彐躅篚猸玢ㄧ孱ㄣ镱í玢轭栝忾舄箦翩玢疱钿轭绐舂铋飑ㄦ戾è疱蜴矧憝玢ī换冕祆邃骝镯咨匀险原敲晌犷咨匀险原晌耘乙招杂换徭翦翳黠蜢栳忮孱篝镳疱洮怩轸犷换狩膑狎潇祜铉痖邈镦泔溴麸铄篝箫溴屦禊戾è镬洵躞徵ㄤ钺黹悱躞徵濠铄鳝躞徵癌篝狎舡糸礤ㄧ弭轭翦蝾犰蝓瞽糸礤┅ㄣ镬戾泗玑蜮徵珏瞟箦翩玢屦镢瑾ㄣ镱癌戾è蝓瞽糸礤ōㄧ弭轭翦蝾犰蝓瞽糸礤篝狎舡糸礤┅换颂漳桥语礤糸礤麇箦翳箦泔钿珏趄躞徵濞汜祆换蝈趱蝾箜犰戾鲠祯翳犷翳骈蝮衄麒殂汜换戾徜麸敲艺苇陨团麸顼轭铄玑糸鲥麒殂轭换趱蝾轶豉疱弪蝻虍麒孱痨躞蝓瞽糸礤ㄩ钽玢蝓瞽糸礤蝓瞽糸礤┅＋ㄡ钿筲翳蝈徜筲筢驽痫轭舂箦翩篝镳骘颦玢疱钿轭绐铋飑箦翩玢疱钿轭绐铋铄鳝躞徵ㄤ钺黹悱躞徵濠＋筲翳蝈徜ㄡ鲥铒篝镳骘颦玢疱钿轭绐┅ㄧ悱篝狎舡翳瀛黠蜢洎换深眭祠轸栩遽溴孱鲩蝻铐孱翳雉桢翳蝈徜换鏖祆箦瞽猸姝锃皙汨犷珏扉趑戾灬翦怩换翳狒纤换萎庐翳秕翦鏖翳秕舡玢轭痱弼孱趔翳轶换骢钽糸镱骝镯忮轭孱翦蝈洮箫铒铄邃骘换祜汶轭绠戾è骝邋ō镬洵躞徵铄鳝躞徵濠┅换桥蚊敲镢汜箝镱犰禊蝈痫螋铄玑糸鲥桢蝈怩换翳沲蝌孱忮扉彐轶翳狒轸轶疳螋镦翳换铒蝽犰矧溴镦翳轭珞犷铒怩绠麒孱痨躞骝邋洎ㄩ钽瞽怡翦蟓骝邋洵矧瘐蜷骈邃骝邋洎┅┅ㄤ邈灬蝈ㄩ铎轭疱蜴矧憝玢┅换体臾磲脲篚蝈麇蝈铒轭翦蝌躔翦犷翳狒铒铄镦换翳溴徜扉铄矧溴徜祜汶溴翦泗轱篝蹑趄殓珏蝮鏖翳秕舡轭翦蝌躔趔戾è筲轫痨汉溴徜扉铄铋飑ㄥ痫汨玢屦镢瑾┅祜镳换敲轭眭篝忮滹铄鏖翳秕舡玢轭麸狯镩换蝈沲蝮轹敲怩麇汜瞌忪镢镱换犰蝈徜轭玢轭箝溴鏖翳秕舡玢轭绾翳狒黠蹯换汜躞溴徜祜汶鏖翳秕舡玢轭换则麸珧徕翳狒眭翦项徙聃轶轸轱瞵篝镳换翳黠蜢骝镯鏖翳翳眭翦桢熹犷翳孱换屮邈豸翳蝈磲轭溴镦翳敲篝镳痖铉翳换黠蜢鏖翳轭翦蝌躔趔溟筢忪邃轶翳盹翳弪镦换犰泸轸殂犰箦泗轱铙ㄣ镱è趄徙聃轵瀛玢祜汶躅筢驽沆遽颦蝻雉珏瞟ㄧ悱篝镳翳瀛黠蜢洎换吁沣弩蟆敲疱蜴矧憝玢换义趱蝾怩戾狯玢疱钿轭绐狍轶麇换溟犰祜汜翦糸铢忾徭翦敲轭绠深换翳屣蝙翳轶泔蹯戾徜麸祜铉汨衢换镦翎殪蝈沲蝮轹ㄢ豸铒轭屮痨殂轸换翎殪痫箝糸镱敲蟋怩翳狒滹弩瞌换箦屙扉脲禊麸栳痧孱麸镦翦町令换翳镬泔溴犰蝈徜篚骀弪邃骝镯翳轶换痱镡戾懋蝈趱蝾舂换语礤雉桢翳蝈徜轶趄轭麸敲渺遽换玢疱钿轭绐麇犰蝈徜腩秣麇麽铘换敲麸栳痧孱犷彘翳弪戾换鏖翳秕舡玢轭骈珲蝈秕翳狒翳黠蜢换轶篝镳痖铉矧趄徵衢町箦翩玢疱钿轭绐铋飑┅换麇牾篝麽铘邃黹铒敲犷敲栳换镢沲蝌邃体狯瀣怩滹瞌屮邈豸徭翦颦玢换栾镫螽换换义趱蝾骘遽簌翦蝾狎祜玳轭翳换蝓铘轫瀹麒孱ㄡ钿ㄥ耢珏癌铄屦镢玢疱钿轭绐┅蝈趱蝾癌┅┅┅┅＋筲翳蝈徜ㄤ彐躅痫篝玢ī筲轫痨汉骈钺扉弪翳蝈徜铒糸纟ㄡ扉孱骢钽犰ㄥ翦蝾犰殄㈠眇豉唪栩遽溥蝈泫沆邂轭ㄦ躅泗轱鲲殇┅换酗篝敲徙糸镱狎轭鲲脲簌钽栩镱秕箪怡翳敲轭翳蝈徜换麒殂轶犷狎忾趄狎镱瀹涉翳矬徙糸镱狁蹰蝈犷祜汶蟋矧狎箦铙轸轹换麸翳篝狒镦撂滔篆咨匀晌耘乙招杂犷溴徜祜汶镦麒狒栳鲥秕换狎躞弪弪蝻虍蕊镫铄邃麸忮篚骀殂殄铘禊躅泔眇扉汜翦狍麸忮栳蝽戾篌ㄣ犰飙栾镫⑨骠弪敲徭翦颦玢栾镫螵猴瞽弪蝻瑚狎瞟－筲翳蝈徜ㄤ彐躅痫篝玢ī换硝趔殇翳眭翦轭翦蝌躔趔磲忮孱徕戾浜翳弩磲汜躞换犷雉桢敲粕赝藕轸汜痫翦铘獒祆屮沐邃磲轫蹴轭翦蝌躔换铄篝轭怡趄殓珏蜷铉敲螽换换冕翳狒忮狯镩溴怡栳鲩铉翳栾镫蝓镱禊换骝镯翳秕翦蝽矬诱颅敲涉翳铄篝邃敲栳痧孱轭轭翦蝌躔换栳钿戾蝮翳狒铒孱秕玷换换颂漳桥娘瞌蝓翳栾镫轭敲殒换换俩翳轶翳蝈徜轶澌轭矧牾篝怙蝾箫翳狒躞弪泔溴铄鲥蝓铙鏖翳换翳蝈徜犰轹瀛沲蝌孱舡翳蝈徜骄铋飚换澡牾篝怙蝾汜箦汜栳痧孱鏖翳骘蝈殓翳蝈徜翳狒狎躅祯汶换孱秕玷麸忮屐邈翦麸疱蜴矧敲牾篝狍翳妁忮玳屮邈豸轭换盼耘噎葡遗汕苇昧烫铝盟澡轶溴骈铋翦禊箦屙麸栳痧孱鏖翳筲筢驽痫轭舢换骚铒篚蝈麒弭桢轸汜栳痧孱鏖翳秕筲筢驽痫轭舢换换漏轭翦蝌躔趔狎溟筢忪邃箫礤麒弪躔翳汜祆汨衢箝钽麇换滹瞌麽铘麸蝓躞弪泔溴轭篚汨汜箦换换蔑钿轸轱俩轶翦篝邃轭翳蝓铘轫狒汜钸轭鲲脲唣矬暨玢ī换蔑钿轸轱漏轶翦篝邃桢蝈换澡祜铉翦蝽箫祯糸镱鏖祆忮麸脲屦箦疳蜥翦翳蝈徜骘换徭翦颦玢栾镫螽换崎钺扉弪狎轭箦疳蜥翦翳蝈徜躞踽祆┈换怩轸铒疱蝽轶箝忪麸轭鲲脲孟文稍上苇蜗陨瀑骝镯换澌轭翳蝈徜箫麇篝殪铄邃翳珲狎骘翳狒怩铒换翳珲狎骘麒弭桢轭翦蝓痿狎孱徕戾洚换儒蝈镱蝈狍镱翳狒土剡晌耘乙招杂轶狍栝玷狍轸轶换涉翳翳蝈徜翳狒疱蜴矧礤敲蝓铙痫篝敲栾镫麒殂泔铙孱秕玷麸换汜躞犷雉桢敲麒殪轭翳栾镫蟋翳孱狍箫镱狍轭翦蝌躔趔狎犰祜麇换徵衢瞵敲汜忮轭鲲脲Ⅱ邈躜箝鲥禊麒殪翳弪轶磲忮哏悒镱换翳汜祆篝徙氘婶铒弼孱趄蹂翳狒唪栝筮翳蝈徜栳麸泔铙麸换犷翳蝈徜泔蹯栳鲥汜躞轭翳轶翳蝈徜麸栝翳敲趄殓珏麒殂箦趔翳换协轭翦蝌躔翦忾麒殂汜躞弩翳轭翦蝌躔轭篝蝓泗轱狒翳孱镦换痼艴滹狒镯殂箦聃孱沐麸翎脲箝珙犰栝舢语鏖翳轭翦蝌躔趔遽钼戾洮换麇珏忉汶轭麸翳敲麒殂汜祆痫篝敲麒殂黹玷泔铙换渝翳屮犴痨狒翳怙趑镯镦篁惘泔溴骈钺骘沆遽痖泗躜瀹换田玳汜祆翳骈钺扉弪屮轶翦钽翦篝忮祜铉轭篁惘泔溴骈钺飕换怩翳骒狩轭翳狒轶麇蝈铒顼轭麸汜祆翳狒泔溴躅糸躅磲箅轭换轭翦蝌躔趔麒殂轶痱邈轶屐翳翳轭麇铄邃麸蜗滹殒犰蝈徜换轭痫篝敲泔溴镦犷腴钿ㄢ轸骈钺扉弪矧雉桢颟麒孱ㄡ钿犰祜鳝鏖翳轭翦蝌躔趔矧ㄡ钿筲轫痨汉栳箬翎忪瀛沲祆邃鲠祯弩筲轫痨汉骈钺扉弪殇磲筲轫痨汉骈钺扉弪篝矧濯┅铒筲轫痨汉轭岘骈钺扉弪┅徭翦颦玢栾镫螵┅筲翳蝈徜汉鏖翳秕舡翳蝈徜麽轸轭绛骘ī鏖翳轭翦蝌躔趔蝓瞽疱钿轭绛骈钺扉弪螬ㄣ犰飙栾镫⑨骠弪敲徭翦颦玢栾镫螵猴瞽弪蝻瑚狎瞟┅┅换澡轶轶翳躞弪徜鲥螋轶邃玑蜮徵泔祆邈糸镱骢钽糸镱ㄤ彐躅玢é脲ㄦ蹯铋飑ㄧ孱癌犰祜鳝雉桢颦脲螬＋珏钽玢⑸铋糸狒玑蜮徵泔祆邈糸镱澡溴驷蹯轶麸轭轸獒翦铛蝮弪泔祆邈糸镱麒殂磲轭趱蝾趄殓珏泔祆邈糸镱镦镱矧盹蝈镬溴珏铄蜥糸镱狍麇祆涉普烫轶趄蹂犰珏铄蜥糸镱狎泔祆邈翦洚涉桥轶痱秭殇邃轸汜忮躞邃麸箴邈殒翳镬溴篝珏铄蜥糸镱珲狎犷翦邃麸忮泔祆邈翦洚项描孱妁敲痨狒骘蝽狎珲礤铘普烫犷桥翎脲铒彐驽泗骢祆泔祆邈糸镱轶犰麽疱蜴矧礤洚－珏钽玢⑸铋糸狒玑蜮徵泔祆邈糸镱澡泔祆邈糸镱轶犰麽骢祆泔祆邈糸镱硫珲礤铘普烫犷桥汜忮躞邃骘泔眇狒殁殪轸鏖翳桥蚊敲痨狒骘蝽蠛翳弪翳溴驷蹯轶麸轭轸獒翦铛蝮弪泔祆邈糸镱麒殂磲轭趱蝾趄殓珏泔祆邈糸镱镦镱矧盹蝈镬溴珏铄蜥糸镱狍麇祆涉普烫轶趄蹂犰珏铄蜥糸镱狎泔祆邈翦洚涉桥轶痱秭殇邃轸汜忮躞邃麸箴邈殒翳镬溴篝珏铄蜥糸镱珲狎犷翦邃麸忮泔祆邈翦洚－珏钽玢ㄤ邈灬蝈ㄩ珙矧骢祆┅戾ǎ珏钽玢ㄧ孱ㄩ骢祆筲鲰韩痼艴滹篝狒殂珏铄蜥糸镱珏瞟┅麒孱ㄥ篚猸玢珏瞟痫篝玢┅┅ㄤ彐轭瀛犰殄瞽蝻豸轭筱蝓猸泔铘蝻飙篝徙鲲殇ㄤ彐躅躅筢驽沆遽颦蝻雉ㄧ孱－珏钽玢ㄤ邈灬蝈ㄩ珙矧珏瞟换颂漳桥娘翳轭珞轭犷狒翦眇麸珏蜷镦屮趄蝻雉螽疹筢驽换狍栳鲩铉翳弩泔铙盹蝈翳犷麇栳鲥箴徙戾骠戾徜麸桴珏换忉漕弩螽筱蝓猸泔铘蝻飙篝徙氅换酗麇汜汨镦翳忾珙蹴痱轭翦蚝潋镳秭弪禊灬蜱忾珙蹴犷换蝈盹鲥漉痨殂狒孱趄殄螽筱蝓猸痫麇颦汜汨濠换渺遽汜汨弩溴疱钿轭镱翳珏铄蜥糸镱忮轭泔祆邈翦洚＋珏钽玢ㄣ镱è羼珏瞟换尿镳篝蜷铉忮汜躞翳栳箬轶痫轭翦颦栳箬换怩翳弪轶铒狨麸磲糸汜汨蝈栳箬轭徭翦敲筲骘蝽狒汉麸脲铋瀛泔铘蝻飙篝蜷铉汜汨瀛沆遽颟è羼珏瞟筲骘蝽狒汉麸脲铋瀛泔铘蝻飙篝蜷铉汜汨瀛沆遽颟ㄣ豉疱镦汜汨瀛沆遽颟ㄤ蝻瓠犰飙栳箬汜汨弩┅－珏钽玢ㄤ蝻瓠犰飙栳箬汜汨弩┅换换狨殪獒蝙骢钽糸镱ㄤ彐躅怡翦蟓泔铙邃忮赭邋瞽玢ī⒃桢犴秕铘镦礤盹蝙翳狒鏖祆忮犰祜汜翦忮骘蝈翳铄玑蜮徵泔祆邈糸镱轶轭轸獒翦洚澡轶汜忮箦鏖翳优云项桥蚊敲痨狒骘蝽翳轶轶翳铛蝮弪箝瀣犷溴驷蹯趔麸单镦澌钺黹箴徙箝瀹物翦沲蝌孱綮汨犷珏麸翳轶鲠祯狎祜篝麒孱筢鲩铉泔蝈ㄥ翦蝾犰殄⑩翦筮泔铙邃哜弭麇孱哏泱矬鲰箝瀛舂ㄤ彐躅箦翩怡翦蟓泔铙邃忮赭邋瞽玢螬鲠飑ㄤ邈灬蝈豉疱ㄡ钿骈铛躅箝珙邃怡翦鲠飑箦翩ㄥ翦蝾犰殄⑩翦筮泔铙邃哜弭麇孱哏泱矬鲰箝瀛舂鲠飑ㄤ邈灬轫ㄩ铎轭磲忮栳钿戾疱钿轭绛玢┅ㄤ彐躅磲忮栳钿戾疱钿轭绛玢ī麒孱ㄡ钿铒玢轭栝忾舄矧＋筲翳蝈徜篝镳骘颦玢疱钿轭绐玢疱钿轭绐┅筲躅轼汉蝈沐轹瀛疱钿轭绛轭翦蝌躔舂┅换换桥蚊敲箴邈殒殂换换ㄤ彐轭瀛犰殄瞽鲠蜷徕戾á玢哽镧骈戾ョ悱祜珂殪濠í汨狎┅ㄤ彐躅箦翩玢祜珂殪濠疳翳钺礤戾è铄麒孱疳翳钺礤磲脲犰殄瞽篝蜷铉钺糸鲥钺礤篝蜷铉趄犷箪狒瀛祜玳汜飙疳翳钺礤疳翳钺礤横蟓骈戾舂┅镬ョ悱祜珂殪濠箦翩ョ悱祜珂殪铄鳗麒孱镬ㄦ蝈瀛犰殄镬洎疳翳钺礤┅ㄤ彐躅玢祜珂殪ī⒁弭躜翳疳翳钺礤躞邃麸祜玑蜮徵泔祆邈糸镱螽冕忮优云腻驷蹯轶紊态礤犷轭泔祆邈糸镱狎铒祜珑邃涉铒瞽铛祆翳溴箝珙狒邃骈戾轶镳孱邃忮骘蝈犷徭翦遽汨泔祆邈糸镱犷珏铄蜥糸镱篝狒轶糸泱狎狃疱钿邃麸轸戾è鲠ㄣ狍ョ悱祜珂殪悱篝蜷铉┅麒孱鲠钺糸鲥疳翳钺礤鲠飑┅ㄤ彐豉疱珏铄蜥糸镱轭溴ī啜轭翦珏筲鲰韩痼艴滹篝狒殂珏铄蜥糸镱┅换粕赝藕桥闻伊陨衔ㄡ钿辛桥狍箦孱轭蝻镯扉箴箬秕熹痱镡徕禊忮换溴骈铄轭涕箴犷黩轸翦麸桢徜弪骈戾怡珏铄箝蟋轭篝遽镦翳轶换狭舷烷铄篌翳轶漉痨殂狒弩翳篝蝓泗溴骈铋糸镱轭珏钽玢惝ㄤ彐轭瀛犰殄瞽豉疱珏铄蜥糸镱篝蝓泗珏铄蜥糸镱ㄢ翦蟓犰祜汜翦矬鲰箝瀛舂ㄧ悱趄殓珏矬鲰箝瀛舂ㄢ翦蟓泔铙邃忮赭邋瞽玢矬鲰箝瀛舂铛礅弪镦玢轭舂铛礅弪镦玢蟓忮骘蝈痱镯雉轱轭舂ㄣ蹴篚憝怡翦蟓犰祜汜翦矬鲰箝瀛舂黹铋眭憝徵瀛忮骘蝈玢滹踱戾┅ㄤ彐轭瀛犰殄瞽鲠蜷徕戾珏铄蜥糸镱ㄡ蝌狴珏铄蜥糸镱．ū筲鲰韩痼艴滹篝狒殂珏铄蜥糸镱┅换阻轶辛桥晌呐丨轭勇伺椅盘怩辛桥犷翳疳珏翎忪狎轭勇滞ㄤ彐轭瀛犰殄瞽豉疱篝蝓泗筲鲰汉疳珏篝蝓泗筲鲰汉疳珏换燥沲滹黝翳箝镦翳疳珏翎忪瀣翳筱犷唧翎螋唢骀箦换岑氙Ⅲ翎螋轶礤狍躜邃轭喘怡翦轭翦珏蝮蝈玑蜾戾篌换镦黠蜾箝瀹澡轶轶骈铄骘巢忾徜潋弩箴徙瀣换怩殒洞忾翳孱麇栳鲥麸筱犰翳鲠祯瀹龄溟糸镱犰禊换翳弪轶驷祆忉汶骘麒孱弼孱翳筱犰邃鲠祯轶麸忾绠筲鲰汉篝狎＋洞忾躅箝珙邃巢－洞忾箝珙邃换冕豸轱詈澡祜忾镦紫夷迎沼拍轶骒徵忾筲鲰汉黠蜾蟓躞邃躅箝珙邃倍┅í轭翳钺礤轶礤盹蝙衢洎筲鲰汉骒徵躅箝珙邃俯翳轶钺礤豉疱轭筲鲰汉珏箝珙邃俯┅ㄤ彐轭瀛犰殄瞽鲠蜷徕戾á疳珏唪徕戾筲鲰吼徵瀛翎忪濠í篝蝓泗筲鲰汉疳珏┅ㄤ邈灬轫ㄩ铎轭筲鲰烘轭洵疳珏轭溴┅ㄤ彐轭瀛犰殄瞽蝻豸轭á屮暨骈钿唣徵暹轭溴筲鲰烘轭洵疳珏轭溴疳珏轭溴ㄡ滗蝈篌躅箝珙邃┅ㄤ彐躅珏铄蜥糸镱镦镡赍泗鏖翳痖铑邃镡赍泗镡赍泗戾舄è徜潋ㄧ弭扉箴镡戥徜潋弩镡赍泗┅疳珏筲鲰烘轭洵疳珏轭溴徜潋┅ㄣ镱è窘疳珏癌箪雉ㄤ弪彐筲鲰吼徵瀛翎忪疳珏筲鲰汉珏瞟＋轫盹忾戾箴徙è轫盹忾戾箴徙瀛徜潋徜潋换由托膛普误滹瞌泔铘衢珏铄蜥糸镱怡翦麒孱箝眇戾骢瞽镡赍泗箦赳徜潋ㄧ弭扉箴镡戥徜潋弩ㄦ躅泔溴桢徜弪镡赍泗┅┅戾è筢ㄩ铘筢祜玑钿悴徜潋筲鲰红秣翎绛磲箅┅┅祜玑钿ㄩㄦ溴骖镡赍泗筢瓠蝈姝筢暴筢瓠蝈姝筢畅ｘ譬┅┅┅ㄥ痫螋ю徵瀛痱雉邈翦洵皓磲泸镬弭è徜潋卷狎ㄡ滗颟啜筢瓠蝈姝ㄥ翦蝾犰殄㈢氵汜蜾唔狎擘簌篝屙狎遽痫轭翦颟祜玑钿ㄡ箬徜潋ōㄩ铘彗弪戾铉翳ū筲鲰虹孱沌悱汜蜾怡翦螬┅ㄥ翦蝾犰殄㈢氵汜蜾唪徕戾唔狍擘轭舂┅磲螂邃鲠飑＋箫骠汜蜾磲螂啜羼鲠癌换组翳痂箝汜痱雉邈糸镱翳弪狎箝铉戾忾骈屐潴疳汶邃轭翳磲螂怡翦换澡忾轶翳汜蜾磲螂忾ㄩ铞弪箦镦痱雉邈翦洵皓－箫骠汜蜾磲螂啜镤漯鲠飑┅ㄤ彐躅疳珏痱雉邈翦洵镡赍泗下逝迷眭篝忮痖铑邃怡汜祆弪铒磲螂邃ㄡ滗颦卷狎ㄧ弭扉箴镡戥徜潋弩镡赍泗┅┅ㄤ彐躅镡赍泗汜蜾磲螂镡戛ㄡ鲥ㄥㄨ遽瓠犰祜汜翦洵镡戛轰钺黹悌换义趱蝾忾舡鲥泗矧鏖翳骘遽汨磲螂邃汜蜾翳镡赍泗轶镱换犷骘遽汨躅磲螂邃汜蜾鏖翳痖铑邃镡赍泗镡戛戾舄è忉箦祜玑钿悴ㄧ弭扉箴镡戥徜潋弩镡戛筲鲰红秣翎绛磲箅┅灬篝ǐ忉箦ū痱轫轸轹瀛镡赍泗箝镡戛┅ㄡ扉珙邃忉箦祜玑钿悴忉箦ū筲鲰虹孱沌悱汜蜾怡翦螬┅ㄡ扉珙邃灬篝祜玑钿悴灬篝ū筲鲰虹孱沌悱汜蜾怡翦螬┅蝈篚祠磲脲狎蜥ū趄躅汜翦ō犰殓铄洵灬篝犰殓铄洵忉箦筲鲰虹孱沌悱汜蜾怡翦螬哄戾礤铘豉疱р轸┅祜镳骘徜潋骝镯犰殓铄洵忉箦麸犰殓铄洵灬篝怡筲鲰虹孱沌悱汜蜾怡翦骘骝镯滹箦翩ㄡ蝈蝈篚祠椹ㄩ磲螂邃ㄡ滗颦卷狎徜潋┅癌┅蝈篚祠┅┅磲泸镬弭è溴箪雉滹镳糸镱犰徜牾篝徕戾啜痱镧ㄤ彐躅簌礅镬殂狒⑶盼乓猎上苇箪雉ㄧ孱弪狒轱瞟滹ㄤ邈灬蝈ㄧ孱弪狒轱瞽轭溴珏铄蜥糸镱┅箪雉ㄤ弪彐珏铄蜥糸镱珏铄蜥糸镱К箪雉┅括麒孱徜牾篝徕戾啜ㄤ彐躅箦翩簌礅镬殂狒⑶盼乓猎上苇箪雉┅鲠祯珏铄蜥糸镱ㄤ邈灬蝈ㄧ孱弪狒轱瞽轭溴珏铄蜥糸镱┅箦翩箪雉ㄤ弪彐珏铄蜥糸镱珏铄蜥糸镱К箪雉鲠祯濠┅┅┅ㄤ彐怡翦蟓泔铙邃忮赭邋瞽玢⑽蹴忮镦怡翦翳狒汜忮犰祜汜翦麸桥闻伊陨衔忮骘蝈翳狒珏铄蜥糸镱轶泔铙殇弪邃骘玑蜮徵泔祆邈糸镱澡轶鲠祯轶礤犷轭珈弩骘珏铄蜥糸镱翳铛蝮弪┖箦沦耘迎孟斡拍屡宰排苇敲轭篝遽洚腻驷蹯轶单镦翳澌钺黹箴徙箝溟鲩溴怡翳铛礅弪镦铒瞽铛蝮弪珏铄蜥糸镱螽冕忮狍箝珙邃麸躞轭优云瘤衢灬忪镱桥蚊敲痨狒骘蝽镱禊砒疱蜷礤铘犰轭翦蜴徙篚怅邈麸汨犷珏舂ㄤ彐黹铋眭憝徵瀛忮骘蝈玢⑼轭轫蹴狯弪徵徵镦镡赍泗犰祜汜翦麸桥闻伊陨衔忮骘蝈翳狒珏铄蜥糸镱轶磲忮玑蜮徵泔祆邈翦洚腻驷蹯轶爱返渝犰箫桥闻伊陨衔林乓燎怒燎女冕忮狍箝珙邃麸躞轭优云瘤衢灬忪镱桥蚊敲痨狒骘蝽镱禊砒疱蜷礤铘犰轭翦蜴徙篚怅邈麸汨犷珏舂ㄤ彐铛礅弪镦玢蟓忮骘蝈痱镯雉轱⑽蹴忮镦糸礤玑蜮徵泔祆邈糸镱轶滹铄镱桥闻伊陨衔忮骘蝈狨麸磲糸痱镯雉轱麸翳铄珏铄蜥糸镱轶趄殓珏蝈洚腻驷蹯轶碑冕忮狍箝珙邃麸躞轭优云瘤衢灬忪镱桥蚊敲痨狒骘蝽镱禊砒疱蜷礤铘犰轭翦蜴徙篚怅邈麸汨犷珏舂ㄤ彐怡翦蟓犰祜汜翦⑽蹴忮镦怡翦犰祜汜翦麸桥闻伊陨衔沲蝌孱綮瘤衢灬忪镱桥蚊敲痨狒骘蝽镱禊砒疱蜷礤铘犰轭翦蜴徙篚怅邈麸汨犷珏ㄤ彐铛礅弪镦玢⑽蹴忮镦糸礤玑蜮徵泔祆邈糸镱栳忮孱滹铄镱桥闻伊陨衔鏖翳秕痱镯雉轱町瘤衢灬忪镱桥蚊敲痨狒骘蝽镱禊砒疱蜷礤铘犰轭翦蜴徙篚怅邈麸汨犷珏ㄤ彐躅珏铄蜥糸镱狯弪徵瀛徵ㄧ孱弪狒轱瞟⒘鲥蜥珏徵镦礤盹蝙犰祜汜翦麸桥闻伊陨衔狯弪徵铛礅弪镦糸礤镡赍泗犰祜汜翦麸翳珏铄蜥糸镱栳鲥箦孱秕铉弪镡赍泗痱镯雉邃麸轸瘤衢灬忪镱桥蚊敲痨狒骘蝽镱禊砒疱蜷礤铘犰轭翦蜴徙篚怅邈麸汨犷珏ㄤ邈灬蝈ㄧ孱弪狒轱瞽轭溴珏铄蜥糸镱┅ㄡ扉孱骢钽犰ㄥ翦蝾犰殄㈢孱弪狒轱钸狯弪徵暹徵澧ㄦ躅泗轱滹踱戾珏铄蜥糸镱轭溴舂珏铄蜥糸镱┅磲泸镬弭è汜箦ī啜泔钿è筲鲰轰钺黹悱箴徙瀛篝狎徜潋筢瓠轭ㄤ钺黹悱箴徙瀛骝邋痫轭翦颟┅轰钺黹悌è轫盹忾戾箴徙瀛徜潋徜潋洪眄镡殪濠è筲鲰候遽洵镱禊箴徙瀛篝狎徜潋筢瓠轭筲鲰邯蝈徜镱禊箴徙瀛骝邋痫轭翦颡┅候遽洵镱禊è筲鲰后翎糸悱箴徙瀛篝狎徜潋筢瓠轭筲鲰邯篝狒殂箴徙瀛骝邋痫轭翦颡┅后翎糸悌┅换义趱蝾趄蹂殒轶轭犷铒瞽篝徙敲磲钺珏箴徙瀹换ㄎ镱篝徙轫痨殄铒蕴铒忾钿轭篝徙氅换澡弪濮黹泸矬泔痖鏖钿秣镦糸礤轭麒殂铄暨骝邋唣徵骘澌钺黹箴徙换泔蹯咪邈蝈狍暹徭翦汜煦蹯狒轭翳徜潋弩镦噩箫麇ъ痖换麸孱篚蝈翳狒汜瞌栳痧孱ㄤ彐躅桢狃犰祜汜翦洵鏖翳痖铑邃镡赍泗戾è徜潋ㄧ弭扉箴镡戥徜潋弩┅ㄡ钿筲鲰洪蟓扉箴痫轭翦徜潋ㄣ狍弩┅┅换深翦蝾犰躞镱禊粕赝藕翳轭翳轶漉痨殂狒弩泔溴翳狒屮轶趔换箫礤麒弪屐箦麒殂泔蹯铒骈钿ㄤ彐躅扉箴箴徙瀛筢狨ㄡ滗筢瓠轭筢皓┅ㄣ狍弩┅孱土靡咸旁ㄤ彐轭瀛泔钿轸轱礤盹蝙驷蹯舡弪蝻簌篝屙泔钿轸轱弪蝻颟īê蝈痫螋灬礅溽ㄣ镱溟糸镱篝蝈犴戾舄è驷蹯翎滗簌篝屙泔钿轸轱瞽徜潋弩泔钿轸轱瞟篝蜷铉ㄩ冀筲鲰候遽洵镱禊箴徙瀛篝狎驷蹯翎滗筢瓠轭筢皤筲鲰邯蝈徜镱禊箴徙瀛骝邋痫轭翦颡暴┅⒘趑屙痿麸盹溟纟蝈徜镱禊镡赍泗狒ｘ禺⒄铊犷潇邃礤盹蝙驷蹯狒ｘ禺┅ㄦ矧磲篝蝈犴篝蜷铉驷蹯翎滗颟┅┅